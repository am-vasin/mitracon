*╔════════════════════════════════════════════════════════════════════════╗
*║ Файл Ord_Note     Разработчик Андрей Васин           20.03.98 13:50:18 ║
*╟────────────────────────────────────────────────────────────────────────╢
*║                                                                        ║
*║                     Примечание к заявке на склад.                      ║
*║                                                                        ║
*╚════════════════════════════════════════════════════════════════════════╝
PROCEDURE Ord_Note
PARAMETERS tit, dt, tm, nm, to_print, queue_t

*
*  Переменные состояния для навигации
*

PRIVATE stat_type     && Тип навигации: 0 - нестандартный;
                                        1 - бланк;
                                        2 - BROWSE - таблица;
                                        3 - BROWSE - список.
PRIVATE what_do       && Имя режима.
PRIVATE menu_name     && Имя асинхронного меню.
PRIVATE last_mouse    && Время последнего нажатия звериной кнопки.
PRIVATE win_name      && Имя окна ( окон для BROWSE ).

*
*   Заполняем значениями переменные состояния...
*
stat_type  = 1
what_do    = ""
menu_name  = ""
last_mouse = 0
win_name   = PROGRAM()

*------------------------------------------------------------------------
*    Содержательная часть программы:
*

*┌──────────────────────────────────────────────────┐
*│         Собрать к ДД.ММ.ГГГГ(F1) ЧЧч ММм         │
*│   ┌─ Примечание ─────────────────────────────┐   │
*│   │ ....:....!....:....!....:....!....:....! │   │
*│   └──────────────────────────────────────────┘   │
*│               [ ] Печать документа               │
*│        < OK Ctrl-W > < Отказаться Esc >          │
*└──────────────────────────────────────────────────┘

PRIVATE ex, p, d, h, m, f1_butt, clr_butt, nm_w, prnt
                     &&   Объявляем и заполняем поля бланка
ex   = 1             &&
d = dt
h = VAL(ALLTRIM(SUBSTR(tm,1,2)))
m = VAL(ALLTRIM(SUBSTR(tm,4,2)))
f1_butt = 1
clr_butt = 1
prnt = to_print
nm_w = nm
*------------------------------------------------------------------------

PUSH KEY CLEAR       && На всякий пожарный случай!
ON KEY LABEL F1 DO Set_Date
DO Prp_Nav_2
DO D_Win_N WITH 9, 54, tit
DO Sun_Bord WITH 3, 5, 5, 48, " Примечание "
@ 2, 11 SAY "Собрать к           (F1)   ч   м"
*------------------------------------------------------------------------
*      Ввод полей бланка
*
IF .NOT. hard_sale
  @ 7, 10 GET ex PICTURE "@*HT \! OK Ctrl-W ;\? Отказаться Esc "
ENDIF
@ 2, 21 GET d PICTURE "@D"
*@ 2, 24 GET f1_butt PICTURE "@*N F1" VALID Set_Date()
@ 2, 36 GET h PICTURE "@Z 99"
@ 2, 40 GET m PICTURE "@Z 99"
*@ 2, 38 GET clr_butt PICTURE "@*N Не собирать" VALID Clr_Ready()
@ 4,  7 GET nm_w
@ 6, 17 GET prnt PICTURE "@*C Печать документа"
IF hard_sale
  @ 7, 10 GET ex PICTURE "@*HT \! OK Ctrl-W ;\? Отказаться Esc "
ENDIF
READ CYCLE VALID Tst_Blank()

IF ex = 1
  to_print = prnt
  nm = nm_w
  IF EMPTY(d)
    dt = {}
    tm = "        "
  ELSE
    dt = d
    tm = RIGHT(STR(100+h),2)+":"+RIGHT(STR(100+m),2)+":00"
  ENDIF
ENDIF
*--------------------------------------------------------------------------

POP KEY
RELEASE WINDOW (win_name)
RETURN ex = 1

*┌────────────────────────────────────────────────────────────────────────┐
*│   Процедура Clr_Ready    Разработчик Андрей Васин                      │
*├────────────────────────────────────────────────────────────────────────┤
*│                                                                        │
*│                        Очистка даты и времени.                         │
*│                                                                        │
*└────────────────────────────────────────────────────────── 05.01.2000 ──┘
PROCEDURE Clr_Ready

d = {}
h = 0
m = 0
SHOW GET d
SHOW GET m
SHOW GET h

RETURN .T.

*┌────────────────────────────────────────────────────────────────────────┐
*│   Процедура Set_Date     Разработчик Андрей Васин                      │
*├────────────────────────────────────────────────────────────────────────┤
*│                                                                        │
*│                      Установка даты по календарю.                      │
*│                                                                        │
*└────────────────────────────────────────────────────────── 05.01.2000 ──┘
PROCEDURE Set_Date

d = Get_Date(d)
SHOW GET d
IF EMPTY(d)
  h = 0
  m = 0
  SHOW GET h
  SHOW GET m
ENDIF

RETURN .T.

*┌────────────────────────────────────────────────────────────────────────┐
*│   Процедура Tst_Blank    Разработчик Андрей Васин                      │
*├────────────────────────────────────────────────────────────────────────┤
*│                                                                        │
*│                     Проверка корректности бланка.                      │
*│                                                                        │
*└────────────────────────────────────────────────────────── 05.01.2000 ──┘
PROCEDURE Tst_Blank

PRIVATE tm_w, mss

IF ex = 2
  RETURN .T.
ENDIF

tm_w = RIGHT(STR(100+h),2)+":"+RIGHT(STR(100+m),2)+":00"
IF EMPTY(d)
  RETURN .T.
ENDIF

IF .NOT. BETWEEN(tm_w, b_stock, e_stock) .OR. .NOT. BETWEEN(m, 0, 59)
  DIMENSION mss(3)
  mss(1) = ""
  mss(2) = CHR(0)+"Вы некорректно указали время, или склад в это время не работает!"
  mss(3) = ""
  DO Out_Mess WITH 7, "mss"
  RETURN .F.
ENDIF
IF .NOT. EMPTY(d) .AND. hard_sale
  IF d # dt .OR. tm_w # tm
    DO Get_Sto WITH queue_t
    IF Delta_T(d, tm_w, DATE(), TIME()) < t_stock
      DIMENSION mss(3)
      mss(1) = ""
      mss(2) = CHR(0)+"Склад не в состоянии собрать заявку к этому времени!"
      mss(3) = ""
      DO Out_Mess WITH 7, "mss"
      RETURN .F.
    ENDIF
  ENDIF
ENDIF

RETURN .T.
