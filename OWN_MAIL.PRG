*╔════════════════════════════════════════════════════════════════════════╗
*║   Имя файла Own_Mail     Разработчик Андрей Васин                      ║
*╟────────────────────────────────────────────────────────────────────────╢
*║                                                                        ║
*║                   Индивидуальные настройки эл. почты                   ║
*║                                                                        ║
*╚══════════════════════════════════════════════════════════ 05.02.2006 ══╝
PROCEDURE Own_Mail

*
*  Переменные состояния для навигации
*

PRIVATE stat_type     && Тип навигации: 0 - нестандартный;
                                        1 - бланк;
                                        2 - BROWSE - таблица;
                                        3 - BROWSE - список.
PRIVATE what_do       && Имя режима.
PRIVATE menu_name     && Имя асинхронного меню.
PRIVATE last_mouse    && Время последнего нажатия звериной кнопки.
PRIVATE win_name      && Имя окна ( окон для BROWSE ).

*
*   Заполняем значениями переменные состояния...
*
m.stat_type  = 1
m.what_do    = ""
m.menu_name  = ""
m.last_mouse = 0
m.win_name   = PROGRAM()

*------------------------------------------------------------------------
*    Содержательная часть программы:
*

*┌────────────────────────────────────────────────────────────────┐
*│ Пользователь ....:....!....:....!....:....!....:....!....:.... │
*│ ┌─ Имя отправителя ──────────────────────────────────────────┐ │
*│ │....:....!....:....!....:....!....:....!....:....!....:....!│ │
*│ └────────────────────────────────────────────────────────────┘ │
*│ ┌─ E-Mail отправителя ───────────────────────────────────────┐ │
*│ │....:....!....:....!....:....!....:....!....:....!....:....!│ │
*│ └────────────────────────────────────────────────────────────┘ │
*│ ┌─ Стандартный текст письма ─────────────────────────────────┐ │
*│ │....:....!....:....!....:....!....:....!....:....!....:....!│ │
*│ │....:....!....:....!....:....!....:....!....:....!....:....!│ │
*│ │....:....!....:....!....:....!....:....!....:....!....:....!│ │
*│ │....:....!....:....!....:....!....:....!....:....!....:....!│ │
*│ │....:....!....:....!....:....!....:....!....:....!....:....!│ │
*│ └────────────────────────────────────────────────────────────┘ │
*│  Заставка: ( ) Выдавать всегда  ( ) Только при необходимости   │
*│               [ ] Отправлять копию на свой адрес               │
*│ Сообщ. об отправке: ( ) Нет  ( ) Только при неудаче  ( ) Всегда│
*│                   < OK Ctrl-W > < Отказаться Esc >             │
*└────────────────────────────────────────────────────────────────┘

PRIVATE ex, tmpAddr, tmpName, tmpText, tmpSw, s_sav, tmpUser, tmpCopy, tmpMess
m.ex = 1
*------------------------------------------------------------------------

PUSH KEY CLEAR       && На всякий пожарный случай!
m.s_sav = SELECT()
SELECT 0
USE (base_path+"PERSONS") SHARED AGAIN ALIAS PRS6205 ORDER TAG CODE
SEEK m.user
m.tmpUser = ALLTRIM(FAMILY)+" "+ALLTRIM(NAME)+" "+ALLTRIM(S_NAME)
m.tmpUser = PADR(m.tmpUser, 49)
IF FSIZE("EML", "PRS6205") = 0
  m.tmpAddr = PADR(PersParm("MAILADDR"), 60)
ELSE
  m.tmpAddr = PRS6205.EML
ENDIF
m.tmpName = PADR(PersParm("MAILNAME"), 60)
m.tmpText = PersParm("MAILTEXT")
m.tmpSw   = VAL(PersParm("MAILMODE"))
m.tmpSw   = MAX(m.tmpSw, 1)
m.tmpMess = VAL(PersParm("MAILMESS"))
m.tmpMess = MAX(m.tmpMess, 1)
m.tmpCopy = .NOT. EMPTY(PersParm("MAILCOPY"))
DO Prp_Nav_2
DO D_Win_N WITH 21, 68, "Индивидуальные настройки эл. почты"
DO Sun_Bord WITH  3,  3,  5, 64, " Имя отправителя "
DO Sun_Bord WITH  6,  3,  8, 64, " E-Mail отправителя "
DO Sun_Bord WITH  9,  3, 15, 64, " Стандартный текст письма "

*------------------------------------------------------------------------
*      Ввод полей бланка
*
@  2,  3 SAY "Пользователь" GET m.tmpUser WHEN .F.
@  4,  4 GET  m.tmpName
@  7,  4 GET  m.tmpAddr VALID Clr_Copy()
@ 10,  4 EDIT m.tmpText SIZE 5, 60 COLOR (SCHEME(1,1))
@ WROWS()-5,  4 SAY "Заставка:" GET m.tmpSw PICTURE "@*RH Выдавать всегда ;Только при необходимости"
@ WROWS()-4, 17 GET tmpCopy PICTURE "@*C Отправлять копию на свой адрес"  ;
                WHEN .NOT. EMPTY(m.tmpAddr)
@ WROWS()-3,  3 SAY "Сообщ. об отправке:" GET m.tmpMess ;
   PICTURE "@*RH Нет ;Только при неудаче ;Всегда"
@ WROWS()-2, FLOOR(WCOLS()/2-16) GET m.ex PICTURE "@*HT \! OK Ctrl-W ;\? Отказаться Esc "

READ CYCLE

IF m.ex = 1
  *
  * Отрабатываем бланк
  IF FSIZE("EML", "PRS6205") = 0
    DO PersParm WITH "MAILADDR", m.tmpAddr
  ELSE
    REPLACE EML WITH m.tmpAddr
  ENDIF
  DO PersParm WITH "MAILNAME", m.tmpName
  DO PersParm WITH "MAILTEXT", m.tmpText
  DO PersParm WITH "MAILMODE", STR(m.tmpSw)
  DO PersParm WITH "MAILMESS", STR(m.tmpMess)
  DO PersParm WITH "MAILCOPY", IIF(tmpCopy, "1", "")
ENDIF
*--------------------------------------------------------------------------

USE
SELECT (m.s_sav)
POP KEY
RELEASE WINDOW (win_name)
RETURN

*┌────────────────────────────────────────────────────────────────────────┐
*│   Процедура Clr_Copy     Разработчик Андрей Васин                      │
*├────────────────────────────────────────────────────────────────────────┤
*│                                                                        │
*│                          Выход из поля адреса                          │
*│                                                                        │
*└────────────────────────────────────────────────────────── 10.04.2006 ──┘
PROCEDURE Clr_Copy

IF EMPTY(m.tmpAddr)
  m.tmpCopy = .F.
  SHOW GET m.tmpCopy
ENDIF

RETURN .T.
