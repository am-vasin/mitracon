*╔═══════════════════════════════════════════════════════════════════════╗
*║                                                                       ║
*║              Допустимые типы платежей для предприятия.                ║
*║                                                                       ║
*╚═══════════════════════════════════════════════════════════════════════╝
PROCEDURE Let_Pay
PARAMETER f_code      && Код предприятия

*
*  Переменные состояния для навигации
*

PRIVATE stat_type     && Тип навигации: 0 - нестандартный;
                                        1 - бланк;
                                        2 - BROWSE - таблица;
                                        3 - BROWSE - список.
PRIVATE what_do       && Имя режима.
PRIVATE menu_name     && Имя асинхронного меню.
PRIVATE last_mouse    && Время последнего нажатия звериной кнопки.
PRIVATE win_name      && Имя окна ( окон для BROWSE ).

*
*   Заполняем значениями переменные состояния...
*
stat_type  = 1
what_do    = ""
menu_name  = ""
last_mouse = 0
win_name   = PROGRAM()

*------------------------------------------------------------------------
*    Содержательная часть программы:
*

*   Открываем файлы БД
PRIVATE ex, tmp_list, p_n, p_bn, p_ue, p_c, p_sb, p_bue
sel_sav = SELECT()

SELECT 0
USE (base_path+"FIRMS") ORDER TAG FIRM_CODE ALIAS L_PAY AGAIN
SEEK f_code
p_n   = .NOT. "Н" $ H_TYPES
p_bn  = .NOT. "1" $ H_TYPES
p_ue  = .NOT. "3" $ H_TYPES
p_c   = .NOT. "Ч" $ H_TYPES
p_sb  = .NOT. "Б" $ H_TYPES
p_bue = .NOT. "4" $ H_TYPES

*    Допустимые типы платежей для предприятия
*┌─────────────────────────────────────────────────────────┐
*│ ┌─ Название предприятия ──────────────────────────────┐ │
*│ │  ....:....!....:....!....:....!....:....!....:....! │ │
*│ └─────────────────────────────────────────────────────┘ │
*│ [ ] Наличные платежи	       [ ] Безналичные платежи     │
*│ [ ] Индексируемые платежи   [ ] Чеки                    │
*│ [ ] Сбербанк	               [ ] Индексируемый Б/Н       │
*│            < OK Ctrl-W > < Отказаться Esc >             │
*└─────────────────────────────────────────────────────────┘

ex   = 1
*------------------------------------------------------------------------

PUSH KEY CLEAR       && На всякий пожарный случай!
DO Prp_Nav_2
DO D_Win_N WITH 10, 61, "Допустимые типы платежей для предприятия"
DO Sun_Bord WITH 2,  3,  4, 57, " Название предприятия "
*------------------------------------------------------------------------
*      Ввод полей бланка
*

@  3,  6 SAY L_NAME
@  5,  3 GET p_n   PICTURE "@*C Наличные платежи"
@  5, 31 GET p_bn  PICTURE "@*C Безналичные платежи"
@  6,  3 GET p_ue  PICTURE "@*C Индексируемые платежи"
@  6, 31 GET p_c   PICTURE "@*C Чеки"
@  7,  3 GET p_sb  PICTURE "@*C Сбербанк"
@  7, 31 GET p_bue PICTURE "@*C Индексируемый Б/Н"
@  8, 14 GET ex PICTURE "@*HT \! OK Ctrl-W ;\? Отказаться Esc "

READ CYCLE

IF ex = 1
  tmp_list = IIF(p_n, "", "Н")+ ;
             IIF(p_bn, "", "1")+ ;
             IIF(p_ue, "", "3")+ ;
             IIF(p_c, "", "Ч")+ ;
             IIF(p_sb, "", "Б")+ ;
             IIF(p_bue, "", "4")
  REPLACE H_TYPES WITH tmp_list
ENDIF
*--------------------------------------------------------------------------

POP KEY
RELEASE WINDOW (win_name)
USE

SELECT (sel_sav)

RETURN
