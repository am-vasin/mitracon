*╔════════════════════════════════════════════════════════════════════════╗
*║   Имя файла Sys_Parm     Разработчик Андрей Васин                      ║
*╟────────────────────────────────────────────────────────────────────────╢
*║                                                                        ║
*║                           Параметры системы.                           ║
*║                                                                        ║
*╚══════════════════════════════════════════════════════════ 02/28/2000 ══╝
PROCEDURE Sys_Parm

*
*  Переменные состояния для навигации
*

PRIVATE stat_type     && Тип навигации: 0 - нестандартный;
                                        1 - бланк;
                                        2 - BROWSE - таблица;
                                        3 - BROWSE - список.
PRIVATE what_do       && Имя режима.
PRIVATE menu_name     && Имя асинхронного меню.
PRIVATE last_mouse    && Время последнего нажатия звериной кнопки.
PRIVATE win_name      && Имя окна ( окон для BROWSE ).

*
*   Заполняем значениями переменные состояния...
*
stat_type  = 1
what_do    = ""
menu_name  = ""
last_mouse = 0
win_name   = PROGRAM()

*------------------------------------------------------------------------
*    Содержательная часть программы:
*

*┌────────────────────────────────────────┐
*│ [ ] Сохранять продажи в общем файле    │
*│ [ ] Сохранять ГТД при приходе товара   │
*│ [ ] Новый формат заявок                │
*│ [ ] Наличие кассового аппарата         │
*│ [ ] Необходимость утверждения заявок   │
*│ Кол-во дней от продажи до возвр. 999   │
*│┌─Предупреждение об изменении цен──────┐│
*││ Возраст списка более 99999 дней      ││
*││ Уменьшение суммы более чем на 99 %   ││
*│└──────────────────────────────────────┘│
*│    < OK Ctrl-W > < Отказаться Esc >    │
*└────────────────────────────────────────┘

PRIVATE ex, sw_0, sw_1, sw_2, sw_3, sw_4, sw_5
sw_0 = .NOT. EMPTY(Get_Sys("COM_SALE"))
sw_1 = .NOT. EMPTY(Get_Sys("SAVE_GTD"))
sw_2 = .NOT. EMPTY(Get_Sys("NEW ORD"))
sw_3 = .NOT. EMPTY(Get_Sys("KKM"))
sw_4 = .NOT. EMPTY(Get_Sys("HARD SALE"))
sw_5 = ALLTRIM(Get_Sys("REV DAYS"))
sw_5 = VAL(sw_5)
IF sw_5 = 0
  sw_5 = 15
ENDIF
sw_6 = ALLTRIM(Get_Sys("SALE BOUND"))
IF EMPTY(sw_6)
  sw_6 = 0
  sw_7 = 0
ELSE
  ex = AT(" ", sw_6)
  sw_7 = VAL(SUBSTR(sw_6, ex+1))
  sw_6 = VAL(LEFT(sw_6, ex-1))
ENDIF

ex = 1
*------------------------------------------------------------------------

PUSH KEY CLEAR       && На всякий пожарный случай!
DO Prp_Nav_2
DO D_Win_N WITH 15, 44, "Параметры системы"
@  8, 2 SAY "┌─Предупреждение об изменении цен──────┐"
@  9, 2 SAY "│ Возраст списка более 99999 дней      │"
@ 10, 2 SAY "│ Уменьшение суммы более чем на 99 %   │"
@ 11, 2 SAY "└──────────────────────────────────────┘"

*------------------------------------------------------------------------
*      Ввод полей бланка
*
@ 2, 3 GET sw_0 PICTURE "@*C Сохранять продажи в общем файле"
@ 3, 3 GET sw_1 PICTURE "@*C Сохранять ГТД при приходе товара"
@ 4, 3 GET sw_2 PICTURE "@*C Новый формат заявок"
@ 5, 3 GET sw_3 PICTURE "@*C Наличие кассового аппарата"
@ 6, 3 GET sw_4 PICTURE "@*C Необходимость утверждения заявок"
@ 7, 3 SAY "Кол-во дней от продажи до возвр." GET sw_5 PICTURE "999"
@ 9,25 GET sw_6 PICTURE "@Z 99999"
@ 10,34 GET sw_7 PICTURE "@Z 99"

@ WROWS()-2, FLOOR(WCOLS()/2-16) GET ex PICTURE "@*HT \! OK Ctrl-W ;\? Отказаться Esc "

READ CYCLE

IF ex = 1
  *
  * Отрабатываем бланк
  DO Put_Sys WITH "COM_SALE", IIF(sw_0, "!", " ")
  DO Put_Sys WITH "SAVE_GTD", IIF(sw_1, "!", " ")
  DO Put_Sys WITH "NEW ORD", IIF(sw_2, "!", " ")
  DO Put_Sys WITH "KKM", IIF(sw_3, "!", " ")
  DO Put_Sys WITH "HARD SALE", IIF(sw_4, "!", " ")
  DO Put_Sys WITH "REV DAYS", STR(sw_5)
  IF sw_6 = 0 .OR. sw_7 = 0
    sw_6 = ""
  ELSE
    sw_6 = ALLTRIM(STR(sw_6))+" "+ALLTRIM(STR(sw_7))
  ENDIF
  DO Put_Sys WITH "SALE BOUND", sw_6
  PRIVATE mss
  DIMENSION mss(4)
  mss(1) = ""
  mss(2) = CHR(0)+"Внимание! Некоторые параметры вступят в действие только"
  mss(3) = CHR(0)+"после перезапуска системы на рабочих местах!           "
  mss(4) = ""
  DO Out_Mess WITH 7, "mss"
ENDIF
*--------------------------------------------------------------------------

POP KEY
RELEASE WINDOW (win_name)
RETURN
